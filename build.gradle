plugins {
	id 'jacoco'
	id 'java'
	id 'maven-publish'
	id 'com.github.jlouns.cpe' version '0.5.0'
	id 'org.jreleaser' version '1.18.0'
}

base {
	group = 'com.github.winter-cardinal'
	archivesBaseName = 'winter-cardinal'
}

version = new groovy.json.JsonSlurper().parseText(file('package.json').text).version

// REPOSITORY
repositories {
	mavenCentral()
}

// JAVA
java {
	withJavadocJar()
	withSourcesJar()
	sourceCompatibility = JavaLanguageVersion.of(17)
	targetCompatibility = JavaLanguageVersion.of(17)
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

javadoc {
	options.locale = 'en_US'
	options.charSet = 'UTF-8'
	options.encoding = 'UTF-8'
	options.jFlags '-Dfile.encoding=UTF-8'
	options.addBooleanOption('Xdoclint:none', true)
}

jar {
	manifest {
		attributes( 'Implementation-Title': base.archivesBaseName )
		attributes( 'Automatic-Module-Name': 'wcardinal' )
		attributes( 'Implementation-Version': version )
	}
}

// PUBLISHING
publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.github.winter-cardinal'
			artifactId = 'winter-cardinal'

			from components.java

			pom {
				name = 'Winter Cardinal'
				description = 'A library for real-time web applications build on top of the Spring framework'
				url = 'https://github.com/winter-cardinal/winter-cardinal'
				inceptionYear = '2019'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = project.findProperty('developer.username')
						name = project.findProperty('developer.name')
					}
				}
				scm {
					connection = 'scm:git:https://github.com/winter-cardinal/winter-cardinal.git'
					developerConnection = 'scm:git:ssh://github.com/winter-cardinal/winter-cardinal.git'
					url = 'https://github.com/winter-cardinal/winter-cardinal'
				}
			}
		}
	}

	repositories {
		maven {
			url = layout.buildDirectory.dir('staging-deploy')
		}
	}
}

jreleaser {
	release {
		github {
			skipTag = true
			skipRelease = true
		}
	}
	signing {
		active = 'ALWAYS'
		armored = true
		verify = true
	}
	deploy {
		maven {
			mavenCentral {
				sonatype {
					active = 'ALWAYS'
					url = 'https://central.sonatype.com/api/v1/publisher'
					stagingRepository('build/staging-deploy')
				}
			}
		}
	}
}

// DEPENDENCIES
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-websocket:3.3.1'
	implementation 'org.jdeferred:jdeferred-core:1.2.6'
	implementation 'com.google.guava:guava:33.2.1-jre'
	implementation 'org.webjars:webjars-locator:0.52'

	testImplementation 'org.springframework.boot:spring-boot-starter-security:3.3.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.3.1'
	testImplementation 'junit:junit:4.13.2'
}

// TEST
jacoco {
	toolVersion = '0.8.10'
}

test {
	exclude 'manual/**'
}

// Java API document
task compileJavaApiDocument( type: Copy, dependsOn: javadoc ) {
	doFirst {
		delete './docs/api/java'
	}

	from "$buildDir/docs/javadoc"
	into './docs/api/java'
}
